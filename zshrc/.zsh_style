local SEPARATOR="%F{250}%B†%f"

autoload -Uz vcs_info
setopt prompt_subst
zstyle ':vcs_info:git:*' check-for-changes true
zstyle ':vcs_info:git:*' stagedstr " %F{154}✓%f"
zstyle ':vcs_info:git:*' unstagedstr " %F{217}×%f"
zstyle ':vcs_info:*' formats " $SEPARATOR %F{151}Branch: %b%f%c%u "
zstyle ':vcs_info:*' actionformats " $SEPARATOR %F{151}%b%f%u %F{245}(now %F{148}%a%F{245})%f"

function zle-line-init zle-keymap-select {

    # Gitを見る。
    vcs_info

    # 表示用の文字列を用意する。
    local SEPARATOR="%F{255}%B† %b%f"

    local PWD="%F{229}%B%0~%b%f"
    local CHARACTER="%F{223}ᓀ%B%(?:ˬ:_))フ%f%b"
    local HOSTNAME=""
    local SYMBOL_CHARACTER="-"
    
    # 外部接続だった場合のプロンプト。
    if [ -n "$SSH_CONNECTION" ]; then
        local __HOSTNAME="`echo $SSH_CONNECTION | awk '{ print $1 }' `"
        HOSTNAME=" %F{245}<%f%F{81}%B%M%b%f ← %F{042}%B$__HOSTNAME%b%f%F{245}>%f"
    fi

    # モードに応じてプロンプトを変更する。
    case $KEYMAP in
    vicmd)
        SYMBOL_CHARACTER="==-"
    ;;
    main|viins)
        SYMBOL_CHARACTER="==▶"
    ;;
        vivis)
            SYMBOL_CHARACTER="==■"
        ;;
        vivli)
            SYMBOL_CHARACTER="==■"
        ;;
  esac
    
    local SYMBOL="%B%F{150}$SYMBOL_CHARACTER%f%b"

    # プロンプトを用意する。
    PROMPT="
 $SEPARATOR%F{242}%f$PWD${vcs_info_msg_0_}$MODE$HOSTNAME$SEPARATOR
 %F{242}%f$SYMBOL "

     # 更新。
    zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select

SPROMPT=" %F{247}%R%f → %F{117}%B%r%b %f: "
